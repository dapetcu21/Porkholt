set(CMAKE_CONFIGURATION_TYPES Debug Release)
set(CMAKE_OSX_SYSROOT iphoneos)
set(CMAKE_OSX_ARCHITECTURES "armv6 armv7")
set(CMAKE_EXE_LINKER_FLAGS
  "-ObjC -lPorkholt_iOS -llua -lz -lpng15 -framework AudioToolbox -framework AVFoundation -framework OpenAL -framework OpenGLES -framework Foundation -framework QuartzCore -framework UIKit"
  )
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -Os")
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -DDEBUG")
  
link_directories(${PH_EXTERNALS}/lib/darwin/ios)
link_directories(${Porkholt_iOS_BINARY_DIR})

include(${PH_ENGINE_PATH}/CMakeLib/Porkholt_IncludeDirs.cmake)

add_executable(${PH_NAME} MACOSX_BUNDLE ${PH_SOURCES} ${PH_HEADERS})
add_dependencies(${PH_NAME} Porkholt_iOS)

if(PH_USE_BOX2D)
  include("${PH_ENGINE_PATH}/CMakeLib/Box2D.cmake")
  link_directories(${Box2D_BINARY_DIR})
  set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -lBox2D")
  add_dependencies(${PH_NAME} Box2D)
endif()

if(NOT DEFINED PH_IOS_CODE_SIGN_IDENTITY)
  set(PH_IOS_CODE_SIGN_IDENTITY "iPhone Developer")
endif()
if(NOT DEFINED PH_IOS_DEPLOYMENT_TARGET)
  set(PH_IOS_DEPLOYMENT_TARGET "3.2")
endif()
if(NOT DEFINED PH_IOS_TARGET_IPHONE)
  set(PH_IOS_TARGET_IPHONE "YES")
endif()
if(NOT DEFINED PH_IOS_TARGET_IPAD)
  set(PH_IOS_TARGET_IPAD "NO")
endif()
if(PH_IOS_TARGET_IPHONE)
  if(PH_IOS_TARGET_IPAD)
    set(PH_IOS_DEV_FAMILY "1,2")
  else()
    set(PH_IOS_DEV_FAMILY "1")
  endif()
else()
  if(PH_IOS_TARGET_IPAD)
    set(PH_IOS_DEV_FAMILY "2")
  else()
    set(PH_IOS_DEV_FAMILY "")
  endif()
endif()

if (NOT DEFINED PH_IOS_INFO_PLIST)
	set(PH_IOS_INFO_PLIST "${PROJECT_SOURCE_DIR}/Info-iOS.plist")
endif()
if (NOT EXISTS ${PH_IOS_INFO_PLIST})
	set(PH_IOS_INFO_PLIST "${PH_ENGINE_PATH}/CMakeLib/Info-iOS.plist")
endif()
if (NOT DEFINED PH_MARKETING_VERSION)
  set(PH_MARKETING_VERSION 1.0)
endif()
if (NOT DEFINED PH_BUILD_NUMBER)
  set(PH_BUILD_NUMBER 1)
endif()
if (NOT DEFINED PH_NAME_IDENTIFIER)
  execute_process(COMMAND bash -c "echo -n \"${PH_NAME}\" | tr -d \"\\ \" "
                  OUTPUT_VARIABLE PH_NAME_IDENTIFIER)
endif()
if (NOT DEFINED PH_BUNDLE_ID)
  if (NOT DEFINED PH_COMPANY_ID)
    set(PH_COMPANY_ID com.mycompany)
  endif()
  set(PH_BUNDLE_ID ${PH_COMPANY_ID}.${PH_NAME_IDENTIFIER})
endif()
if (NOT DEFINED PH_COPYRIGHT)
  set(PH_COPYRIGHT "Copyright Â© 2012 __MyCompanyName__. All rights reserved.")
endif()


set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_CODE_SIGN_IDENTITY ${PH_IOS_CODE_SIGN_IDENTITY})
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_GCC_VERSION "com.apple.compilers.llvmgcc42")
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_GCC_THUMB_SUPPORT "NO")
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_GCC_GENERATE_DEBUGGING_SYMBOLS "YES")
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_GCC_SYMBOLS_PRIVATE_EXTERN "YES")
set(PH_IOS_INFO_PLIST_OUT "${CMAKE_CURRENT_BINARY_DIR}/Info-iOS.plist")
configure_file(${PH_IOS_INFO_PLIST} ${PH_IOS_INFO_PLIST_OUT})
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_INFOPLIST_FILE ${PH_IOS_INFO_PLIST_OUT} )
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_IPHONEOS_DEPLOYMENT_TARGET "${PH_IOS_DEPLOYMENT_TARGET}")
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_TARGETED_DEVICE_FAMILY "${PH_IOS_DEV_FAMILY}")
set_target_properties(${PH_NAME} PROPERTIES XCODE_ATTRIBUTE_GCC_C_LANGUAGE_STANDARD "c99")


set(RES_SRC_DIR ${CMAKE_CURRENT_SOURCE_DIR}/rsrc)
set(APP_NAME \${TARGET_BUILD_DIR}/\${FULL_PRODUCT_NAME})
add_custom_command(
  TARGET ${PH_NAME}
  POST_BUILD
  COMMAND ${PH_EXTERNALS}/lua/src/lua ${PH_ENGINE_PATH}/CMakeLib/postprocess.lua ${RES_SRC_DIR} ${APP_NAME}/rsrc "build" ${PH_EXTERNALS}
  )
